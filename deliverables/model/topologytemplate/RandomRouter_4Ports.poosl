import "../common/Flit.poosl"
import "../libraries/distributions.poosl"

process class RandomRouter_4Ports(ProcessingTime : Real, MyNI: Integer)

ports
	InNode, 
	In1, 
	In2,
	In3,
	In4,
	OutNode, 
	Out1, 
	Out2,
	Out3,
	Out4
	
messages

	InNode?Flit(Flit)
	In1?Flit(Flit)
	In2?Flit(Flit)
	In3?Flit(Flit)
	In4?Flit(Flit)
	OutNode!Flit(Flit)
	Out1!Flit(Flit)
	Out2!Flit(Flit)
	Out3!Flit(Flit)
	Out4!Flit(Flit)
	
variables
	RoutingDistribution: Distribution

init
	Initialise()()

methods

	Initialise()()
		RoutingDistribution := new(DiscreteUniform) withParameters(1, 4);
		TransferFlit()()


	TransferFlit()() 
	|f: Flit, i: Integer|
		
		sel
			InNode?Flit(f)
		or
			In1?Flit(f)
		or
			In2?Flit(f)
		or
			In3?Flit(f)
		or
			In4?Flit(f)
		les;
		
		delay(ProcessingTime);
		
		// some routing logic should go here
		// this example router is not very smart and only recognises flits for the local node
		if f getDestination() == MyNI then
			// deliver the flit to a random other router
			
			OutNode!Flit(f)
		else 
			// pick a random router 
			i := RoutingDistribution sample();
			if i==1 then
				Out1!Flit(f)
			else if i==2 then 
				Out2!Flit(f)
			else if i==3 then 
				Out3!Flit(f)
			else if i==4 then 
				Out4!Flit(f)
			fi fi fi fi
		fi;
		
		TransferFlit()()		
		

